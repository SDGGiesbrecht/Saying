thing (clients)
 [
  [
   English: A position of a Unicode scalar.
  ]
 ]
 (
  English: Unicode scalar index
 )
 Swift: “String.UnicodeScalarView.Index”
 {
  part (unit)
   (
    English: index
   )
   platform fixed‐width integer
 }

action
 [
  [
   English: Wraps a platform index on platforms that use integers; returns a nonsense index elsewhere.
  ]
  [
   English: Only intended for internal use by {Unicode scalars} to create its own indices. In general, indices should be obtained from the collection instead.
  ]
  test {
   let (• index: Unicode scalar index) be (Unicode scalar index at (example: platform fixed‐width integer))
   verify ((index) is (index))
  }
 ]
 (
  English: Unicode scalar index at (index: platform fixed‐width integer)
 )
 Unicode scalar index
 Swift: “¤(22)¤(22).startIndex”
 create

use (clients)
 comparison of (Unicode scalar index) for equality
 {
  action (clients)
   (first: Unicode scalar index) is (second: Unicode scalar index)
   truth value
   Swift: “” first “ == ” second “”
   {
    ← ((index) of (first)) is ((index) of (second))
   }

  action (clients)
   (first: Unicode scalar index) is not (second: Unicode scalar index)
   truth value
   Swift: “” first “ ¤(21)= ” second “”
   {
    ← not ((first) is (second))
   }

  action (clients)
   example
   Unicode scalar index
   Swift: “¤(22) ¤(22).startIndex”
   {
    ← Unicode scalar index at (example: platform fixed‐width integer)
   }

  action (clients)
   differing example
   Unicode scalar index
   {
    ← greater example
   }
 }

use (clients)
 comparison of (optional (Unicode scalar index)) for equality
 {
  action (clients)
   (first: optional (Unicode scalar index)) is (second: optional (Unicode scalar index))
   truth value
   Swift: “” first “ == ” second “”
   {
    ← (first) is (second) according to optional
   }

  action (clients)
   (first: optional (Unicode scalar index)) is not (second: optional (Unicode scalar index))
   truth value
   Swift: “” first “ ¤(21)= ” second “”
   {
    ← (first) is not (second) according to optional
   }

  action (clients)
   example
   optional (Unicode scalar index)
   {
    ← wrap (example: Unicode scalar index) as (something)
   }

  action (clients)
   differing example
   optional (Unicode scalar index)
   {
    ← wrap (differing example: Unicode scalar index) as (something)
   }
 }

use (clients)
 comparison of (Unicode scalar index) for order
 {
  action (clients)
   (first: Unicode scalar index) is less than (second: Unicode scalar index)
   truth value
   Swift: “” first “ < ” second “”
   {
    ← ((index) of (first)) is less than ((index) of (second))
   }

  action (clients)
   (first: Unicode scalar index) is greater than (second: Unicode scalar index)
   truth value
   Swift: “” first “ > ” second “”
   {
    ← ((index) of (first)) is greater than ((index) of (second))
   }

  action (clients)
   (first: Unicode scalar index) is less than or equal to (second: Unicode scalar index)
   truth value
   Swift: “” first “ <= ” second “”
   {
    ← ((index) of (first)) is less than or equal to ((index) of (second))
   }

  action (clients)
   (first: Unicode scalar index) is greater than or equal to (second: Unicode scalar index)
   truth value
   Swift: “” first “ >= ” second “”
   {
    ← ((index) of (first)) is greater than or equal to ((index) of (second))
   }

  action (clients)
   greater example
   Unicode scalar index
   Swift: “¤(22) ¤(22).endIndex”
   {
    ← Unicode scalar index at (greater example: platform fixed‐width integer)
   }
 }
