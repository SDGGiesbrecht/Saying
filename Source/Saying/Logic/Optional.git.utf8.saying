extension
 general containers of (thing)
 {
  enumeration (clients)
   (
    English: optional (thing)
   )
   Kotlin: “” thing “?”
   Swift: “” thing “?”
   {
    case
     [
      test {verify ((wrap (true) into optional) is case (something))}
      test {
       if (wrap (true) into optional) is (something), unwrap it as (• unwrapped: truth value), {
        ignore (unwrapped)
       }
      }
     ]
     (
      English: something
      Deutsch: etwas
      français : quelque chose
      ελληνικά: κάτι
     )
     thing
     JavaScript: “” value “”/“let ” unique (resolved optional) “ = ” enumeration “; if (” unique (resolved optional) “ !== null) { const ” value “ = ” unique (resolved optional) “; {” consequence “}}”/“” enumeration “ !== null”
     Kotlin: “” value “”/“val ” unique (resolved optional) “ = ” enumeration “; if (” unique (resolved optional) “ != null) { val ” value “ = ” unique (resolved optional) “;” consequence “}”/“” enumeration “ != null”
     Swift: “” value “ as ” thing “?”/“if let ” value “ = ” enumeration “ {” consequence “}”/“” enumeration “ != nil”

    case
     (
      English: nothing
      Deutsch: nichts
      français : rien
      ελληνικά: τίποτα
     )
     JavaScript: “null”
     Kotlin: “null”
     Swift: “nil”
   }

  use (clients)
   general use of (optional (thing))
   {
   }

  action (clients)
   [
    test {verify (not ((nothing: optional (thing)) exists))}
   ]
   (
    English: (thing: optional (thing)) exists
    Deutsch: (Ding: [thing]) da ist
    français : (chose: [thing]) existe
    ελληνικά: υπάρχει (πράγμα: [thing])
    עברית: יש (דבר: [thing])
   )
   truth value
   JavaScript: “” thing “ !== null”
   Kotlin: “” thing “ != null”
   Swift: “” thing “ != nil”
   {
    ← (thing) is case (something)
   }

  action (clients)
   [
    test {verify ((nothing: optional (thing)) does not exist)}
   ]
   (
    English: (thing: optional (thing)) does not exist
    Deutsch: (Ding: [thing]) nicht da ist
    français : (chose: [thing]) n’ existe pas
    ελληνικά: δεν υπάρχει (πράγμα: [thing])
    עברית: אין (דבר: [thing])
   )
   truth value
   JavaScript: “” thing “ === null”
   Kotlin: “” thing “ == null”
   Swift: “” thing “ == nil”
   {
    ← (thing) is case (nothing)
   }

  flow (clients)
   (
    English: if (optional: optional (thing)) exists, unwrap it as (unwrapped: • thing), (consequence: {})
   )
   JavaScript: “let ” unique (resolved optional) “ = ” optional “; if (” unique (resolved optional) “ !== null) { const ” unwrapped “ = ” unique (resolved optional) “; {” consequence “}}”
   Kotlin: “val ” unique (resolved optional) “ = ” optional “; if (” unique (resolved optional) “ != null) { val ” unwrapped “ = ” unique (resolved optional) “” consequence “}”
   Swift: “if let ” unwrapped “ = ” optional “ {” consequence “}”
   {
    if (optional) is (something), unwrap it as (• unwrapped), {
     consequence
    }
   }

  action (clients)
   (
    English: wrap (value: thing) into optional
   )
   optional (thing)
   JavaScript: “” value “”
   Kotlin: “” value “”
   Swift: “” value “”
   {
    ← wrap (value) as (something)
   }

  flow (clients)
   (
    English: change (storage: ↓ optional (thing)) to (value: thing)
   )
   {
    change (↓ storage) to (wrap (value) into optional)
   }
 }
