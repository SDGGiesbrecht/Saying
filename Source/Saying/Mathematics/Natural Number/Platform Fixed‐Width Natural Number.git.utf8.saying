thing (clients)
 [
  [
   English: A natural number stored in a fixed amound of memory.
  ]
  [
   English: It is generally recommended to use the simpler {natural number} and {integer} instead.
  ]
  [
   English: As fixed‐width representations, {platform fixed‐width natural number} and {platform fixed‐width integer} can be more efficiently processed and stored, but they can only represent a limited number of values. Using them requires consideration of overflow.
  ]
  [
   English: The actual size of {platform fixed‐width natural number} and {platform fixed‐width integer} are determined by each platform’s individual recommendation.
  ]
 ]
 (
  English: platform fixed‐width natural number
 )
 C: “unsigned”
 C♯: “uint”
 Kotlin: “UInt”
 Swift: “UInt”

use (clients)
 general containers of (platform fixed‐width natural number)
 {
  action (clients)
   example
   platform fixed‐width natural number
   {
    ← zero
   }
 }

use (clients)
 comparison of (platform fixed‐width natural number) for equality
 {
  action (clients)
   (first: platform fixed‐width natural number) is (second: platform fixed‐width natural number)
   truth value
   C: “” first “ == ” second “”
   C♯: “” first “ == ” second “”
   JavaScript: “” first “ == ” second “”
   Kotlin: “” first “ == ” second “”
   Swift: «» first “ == ” second „“

  action (clients)
   (first: platform fixed‐width natural number) is not (second: platform fixed‐width natural number)
   truth value
   C: “” first “ != ” second “”
   C♯: “” first “ != ” second “”
   JavaScript: “” first “ != ” second “”
   Kotlin: “” first “ != ” second “”
   Swift: «» first “ != ” second „“

  action (clients)
   hash key (key: platform fixed‐width natural number) with (hasher: ↓ hasher)
   Swift: “” key “.hash(into: ¤(26)” hasher “)”
   {
    hash part (hashing proxy of (key)) with (↓ hasher)
   }

  action (unit)
   (
    English: hash value of (key: platform fixed‐width natural number)
   )
   hash value
   C♯: “” key “.GetHashCode()”
   Kotlin: “” key “.hashCode()”
   {
    ← hash value of (key) according to comparison for equality
   }

  action (clients)
   differing example
   platform fixed‐width natural number
   {
    ← greater example
   }
 }

action (file)
 [
  test {ignore (hashing proxy of (zero: platform fixed‐width natural number))}
 ]
 hashing proxy of (number: platform fixed‐width natural number)
 platform fixed‐width integer
 C: “(unsigned)” number “”
 C♯: “” number “.GetHashCode()”
 JavaScript: “” number “”
 Kotlin: “” number “.hashCode()”
 Swift: “Int(bitPattern: ” number “)”

use (clients)
 comparison of (platform fixed‐width natural number) for order
 {
  action (clients)
   (first: platform fixed‐width natural number) is less than (second: platform fixed‐width natural number)
   truth value
   C: “” first “ < ” second “”
   C♯: “” first “ < ” second “”
   JavaScript: “” first “ < ” second “”
   Kotlin: “” first “ < ” second “”
   Swift: «» first “ < ” second „“

  action (clients)
   (first: platform fixed‐width natural number) is greater than (second: platform fixed‐width natural number)
   truth value
   C: “” first “ > ” second “”
   C♯: “” first “ > ” second “”
   JavaScript: “” first “ > ” second “”
   Kotlin: “” first “ > ” second “”
   Swift: «» first “ > ” second „“

  action (clients)
   (first: platform fixed‐width natural number) is less than or equal to (second: platform fixed‐width natural number)
   truth value
   C: “” first “ <= ” second “”
   C♯: “” first “ <= ” second “”
   JavaScript: “” first “ <= ” second “”
   Kotlin: “” first “ <= ” second “”
   Swift: «» first “ <= ” second „“

  action (clients)
   (first: platform fixed‐width natural number) is greater than or equal to (second: platform fixed‐width natural number)
   truth value
   C: “” first “ >= ” second “”
   C♯: “” first “ >= ” second “”
   JavaScript: “” first “ >= ” second “”
   Kotlin: “” first “ >= ” second “”
   Swift: «» first “ >= ” second „“

  action (clients)
   greater example
   platform fixed‐width natural number
   {
    ← one
   }
 }

use (clients)
 fixed‐width arithmetic with (platform fixed‐width natural number)
 {
  action (clients)
   (
    English: maximum representable
   )
   platform fixed‐width natural number
   C: “UINT¤(5F)MAX” (“limits”)
   C♯: “uint.MaxValue” (“System”)
   JavaScript: “Number.MAX¤(5F)SAFE¤(5F)INTEGER”
   Kotlin: “UInt.MAX¤(5F)VALUE”
   Swift: “UInt.max”

  action (clients)
   (
    English: minimum representable
   )
   platform fixed‐width natural number
   C♯: “uint.MinValue” (“System”)
   Kotlin: “UInt.MIN¤(5F)VALUE”
   Swift: “UInt.min”
   {
    ← “0”
   }

  action (clients)
   (
    English: add (addend: platform fixed‐width natural number) to (cumulative sum: ↓ platform fixed‐width natural number) skipping overflow check
   )
   C: “” cumulative sum “ += ” addend “”
   C♯: “” cumulative sum “ += ” addend “”
   JavaScript: “” cumulative sum “ += ” addend “”
   Kotlin: “” cumulative sum “ += ” addend “”
   Swift: “” cumulative sum “ += ” addend “”

  action (clients)
   (
    English: (augend: platform fixed‐width natural number) plus (addend: platform fixed‐width natural number), skipping overflow check
   )
   platform fixed‐width natural number
   C: “” augend “ + ” addend “”
   C♯: “” augend “ + ” addend “”
   JavaScript: “” augend “ + ” addend “”
   Kotlin: “” augend “ + ” addend “”
   Swift: “” augend “ ¤(26)+ ” addend “”

  action (clients)
   (
    English: can add (addend: platform fixed‐width natural number) to (augend: platform fixed‐width natural number)
   )
   truth value
   {
    ← (augend) is less than or equal to ((maximum representable: platform fixed‐width natural number) minus (addend) skipping overflow check)
   }

  action (clients)
   (
    English: subract (minuend: platform fixed‐width natural number) from (value: ↓ platform fixed‐width natural number) skipping overflow check
   )
   C: “” value “ ¤(2D)= ” minuend “”
   C♯: “” value “ ¤(2D)= ” minuend “”
   JavaScript: “” value “ ¤(2D)= ” minuend “”
   Kotlin: “” value “ ¤(2D)= ” minuend “”
   Swift: “” value “ ¤(2D)= ” minuend “”

  action (clients)
   (
    English: (minuend: platform fixed‐width natural number) minus (subtrahend: platform fixed‐width natural number), skipping overflow check
   )
   platform fixed‐width natural number
   C: “” minuend “ ¤(2D) ” subtrahend “”
   C♯: “” minuend “ ¤(2D) ” subtrahend “”
   JavaScript: “” minuend “ ¤(2D) ” subtrahend “”
   Kotlin: “” minuend “ ¤(2D) ” subtrahend “”
   Swift: “” minuend “ ¤(26)¤(2D) ” subtrahend “”

  action (clients)
   (
    English: can subract (subtrahend: platform fixed‐width natural number) from (minuend: platform fixed‐width natural number)
   )
   truth value
   {
    ← (minuend) is less than or equal to (subtrahend)
   }

  action (clients)
   (
    English: example augend
   )
   platform fixed‐width natural number
   {
    ← example augend according to natural arithmetic
   }

  action (clients)
   (
    English: example addend
   )
   platform fixed‐width natural number
   {
    ← example addend according to natural arithmetic
   }

  action (clients)
   (
    English: example sum
   )
   platform fixed‐width natural number
   {
    ← example sum according to natural arithmetic
   }
 }

use (clients)
 natural arithmetic with (platform fixed‐width natural number)
 {
  action (clients)
   (
    English: zero
   )
   platform fixed‐width natural number
   Swift: “UInt.zero”
   {
    ← “0”
   }

  action (clients)
   (
    English: one
   )
   platform fixed‐width natural number
   {
    ← “1”
   }
 }

use (clients)
 incrementation of (platform fixed‐width natural number)
 {
  action (clients)
   (
    English: increment (value: ↓ platform fixed‐width natural number)
   )
   C: “++” value “”
   C♯: “++” value “”
   JavaScript: “++” value “”
   Kotlin: “++” value “”
   Swift: “” value “ += 1”

  action (clients)
   (
    English: decrement (value: ↓ platform fixed‐width natural number) skipping underflow check
   )
   C: “¤(2D)¤(2D)” value “”
   C♯: “¤(2D)¤(2D)” value “”
   JavaScript: “¤(2D)¤(2D)” value “”
   Kotlin: “¤(2D)¤(2D)” value “”
   Swift: “” value “ ¤(2D)= 1”

  action (clients)
   (
    English: can decrement (value: platform fixed‐width natural number)
   )
   truth value
   {
    ← (value) is greater than (zero: platform fixed‐width natural number)
   }

  action (clients)
   (
    English: successor example
   )
   platform fixed‐width natural number
   {
    ← one
   }
 }
