thing (clients)
 [
  [
   English: A natural number stored in a fixed amound of memory.
  ]
  [
   English: {platform fixed‐width integer} makes the same design tradeoffs as {platform fixed‐width natural number}. See {platform fixed‐width natural number} for more information.
  ]
 ]
 (
  English: platform fixed‐width integer
 )
 C: “int”
 C♯: “int”
 Kotlin: “Int”
 Swift: “Int”

use (clients)
 general containers of (platform fixed‐width integer)
 {
  action (clients)
   example
   platform fixed‐width integer
   {
    ← zero
   }
 }

use (clients)
 comparison of (platform fixed‐width integer) for equality
 {
  action (clients)
   (first: platform fixed‐width integer) is (second: platform fixed‐width integer)
   truth value
   C: “” first “ == ” second “”
   C♯: “” first “ == ” second “”
   JavaScript: “” first “ == ” second “”
   Kotlin: “” first “ == ” second “”
   Swift: «» first “ == ” second „“

  action (clients)
   (first: platform fixed‐width integer) is not (second: platform fixed‐width integer)
   truth value
   C: “” first “ != ” second “”
   C♯: “” first “ != ” second “”
   JavaScript: “” first “ != ” second “”
   Kotlin: “” first “ != ” second “”
   Swift: «» first “ != ” second „“

  action (clients)
   hash key (key: platform fixed‐width integer) with (hasher: ↓ hasher)
   Swift: “” key “.hash(into: ¤(26)” hasher “)”
   {
    feed (direct hash value of (key)) into (↓ hasher)
   }

  action (unit)
   (
    English: hash value of (key: platform fixed‐width integer)
   )
   hash value
   C♯: “” key “.GetHashCode()”
   Kotlin: “” key “.hashCode()”
   {
    ← hash value of (key) according to comparison for equality
   }

  action (clients)
   differing example
   platform fixed‐width integer
   {
    ← greater example
   }
 }

action (file)
 [
  test {ignore (direct hash value of (zero: platform fixed‐width integer))}
 ]
 direct hash value of (integer: platform fixed‐width integer)
 hash value
 C: “(unsigned)” integer “”
 C♯: “” integer “”
 JavaScript: “” integer “”
 Kotlin: “” integer “”
 Swift: “” integer “”

use (clients)
 comparison of (platform fixed‐width integer) for order
 {
  action (clients)
   (first: platform fixed‐width integer) is less than (second: platform fixed‐width integer)
   truth value
   C: “” first “ < ” second “”
   C♯: “” first “ < ” second “”
   JavaScript: “” first “ < ” second “”
   Kotlin: “” first “ < ” second “”
   Swift: «» first “ < ” second „“

  action (clients)
   (first: platform fixed‐width integer) is greater than (second: platform fixed‐width integer)
   truth value
   C: “” first “ > ” second “”
   C♯: “” first “ > ” second “”
   JavaScript: “” first “ > ” second “”
   Kotlin: “” first “ > ” second “”
   Swift: «» first “ > ” second „“

  action (clients)
   (first: platform fixed‐width integer) is less than or equal to (second: platform fixed‐width integer)
   truth value
   C: “” first “ <= ” second “”
   C♯: “” first “ <= ” second “”
   JavaScript: “” first “ <= ” second “”
   Kotlin: “” first “ <= ” second “”
   Swift: «» first “ <= ” second „“

  action (clients)
   (first: platform fixed‐width integer) is greater than or equal to (second: platform fixed‐width integer)
   truth value
   C: “” first “ >= ” second “”
   C♯: “” first “ >= ” second “”
   JavaScript: “” first “ >= ” second “”
   Kotlin: “” first “ >= ” second “”
   Swift: «» first “ >= ” second „“

  action (clients)
   greater example
   platform fixed‐width integer
   {
    ← one
   }
 }

use (clients)
 additive arithmetic with (platform fixed‐width integer)
 {
  action (clients)
   (
    English: zero
   )
   platform fixed‐width integer
   {
    ← “0”
   }

  action (clients)
   (
    English: one
   )
   platform fixed‐width integer
   {
    ← “1”
   }
 }

use (clients)
 incrementation of (platform fixed‐width integer)
 {
  action (clients)
   (
    English: increment (value: ↓ platform fixed‐width integer)
   )
   C: “++” value “”
   C♯: “++” value “”
   JavaScript: “++” value “”
   Kotlin: “++” value “”
   Swift: “” value “ += 1”

  action (clients)
   (
    English: decrement (value: ↓ platform fixed‐width integer) skipping underflow check
   )
   C: “¤(2D)¤(2D)” value “”
   C♯: “¤(2D)¤(2D)” value “”
   JavaScript: “¤(2D)¤(2D)” value “”
   Kotlin: “¤(2D)¤(2D)” value “”
   Swift: “” value “ ¤(2D)= 1”

  action (clients)
   (
    English: can decrement (value: platform fixed‐width integer)
   )
   truth value
   {
    ← true
   }

  action (clients)
   (
    English: successor example
   )
   platform fixed‐width integer
   {
    ← one
   }
 }
