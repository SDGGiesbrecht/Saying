extension
 general containers of (thing)
 {
  thing (clients)
   (
    English: list of (thing)
    Deutsch: Liste von (Ding: [thing])
    français : liste de (chose: [thing])
    ελληνικά: κατάλογος (πράγμα: [thing])
    עברית: רשימה של (דבר: [thing])
   )
   C: “GArray¤(2A)”/“g¤(5F)array¤(5F)ref(” array “)”/“g¤(5F)array¤(5F)unref(” array “)” (“glib¤(2D)2.0”)
   C♯: “List<” thing “>” (“System.Collections.Generic”)
   Kotlin: “List<” thing “>”
   Swift: “[” thing “]”
   {
   }

  use (clients)
   use of (list of (thing)) as list of (thing) indexed by (list index)
   {
    action (clients)
     (
      English: beginning of (list: list of (thing))
     )
     list index
     C: “0”
     C♯: “0”
     JavaScript: “0”
     Kotlin: “0”
     Swift: “” list “.startIndex”

    action (clients)
     (
      English: end of (list: list of (thing))
     )
     list index
     C: “” list “¤(2D)>len”
     C♯: “” list “.Count”
     JavaScript: “” list “.length”
     Kotlin: “” list “.size”
     Swift: “” list “.endIndex”

    action (clients)
     (
      English: advance (index: ↓ list index) to next in (list: list of (thing))
     )
     C: “++” index “”
     C♯: “++” index “”
     JavaScript: “++” index “”
     Kotlin: “++” index “”
     Swift: “” list “.formIndex(after: ¤(26)” index “)”

    action (clients)
     (
      English: index after (index: list index) in (list: list of (thing))
     )
     list index
     C: “” index “ + 1”
     C♯: “” index “ + 1”
     JavaScript: “” index “ + 1”
     Kotlin: “” index “ + 1”
     Swift: “” list “.index(after: ” index “)”

    action (clients)
     (
      English: entry at (index: list index) in (list: list of (thing))
     )
     thing
     C: “g¤(5F)array¤(5F)index(” list “, ” thing “, ” index “)”
     C♯: “” list “[” index “]”
     JavaScript: “” list “[” index “]”
     Kotlin: “” list “[” index “]”
     Swift: “” list “[” index “]”

    action (clients)
     (
      English: first in (list: list of (thing))
     )
     optional (thing)
     Kotlin: “” list “.firstOrNull()”
     Swift: “” list “.first”
     {
      ← first in (list) according to default use as list
     }

    action (clients)
     (
      English: (list: list of (thing)) is empty
     )
     truth value
     Kotlin: “” list “.isEmpty()”
     Swift: “” list “.isEmpty”
     {
      ← (list) is empty according to default use as list
     }

    action (clients)
     (
      English: (list: list of (thing)) is not empty
     )
     truth value
     Kotlin: “” list “.isNotEmpty()”
     {
      ← (list) is not empty according to default use as list
     }

    action (clients)
     (
      English: non‐empty example
     )
     list of (thing)
     {
      ← non‐empty example according to use as changeable list
     }
   }

  use (clients)
   use of (list of (thing)) as changeable list of (thing) indexed by (list index)
   {
    action (clients)
     (
      English: empty
     )
     list of (thing)
     C: “g¤(5F)array¤(5F)new(FALSE, FALSE, sizeof(” thing “))”
     C♯: “new List<” thing “>()”
     JavaScript: “[]”
     Kotlin: “listOf()”
     Swift: “[]”

    action (clients)
     (
      English: append (element: thing) to (list: ↓ list of (thing))
     )
     C: “g¤(5F)array¤(5F)append¤(5F)val(” list “, ” element “)”
     C♯: “” list “.Add(” element “)”
     JavaScript: “” list “.push(” element “)”
     Kotlin: “” list “ += listOf(” element “)”
     Swift: “” list “.append(” element “)”
   }
 }
